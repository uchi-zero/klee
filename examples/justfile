
gclang_config := 'CC=gclang CXX=gclang++ CFLAGS="-g -O0 -Xclang -disable-O0-optnone"'

gnumake version="4.4":
    #!/usr/bin/env bash
    set -euxo pipefail
    if [ ! -d "gnumake" ]; then
        curl -fL -O https://mirrors.ibiblio.org/gnu/make/make-{{version}}.tar.gz
        tar zxf make-{{version}}.tar.gz
        mv make-{{version}} gnumake
        rm make-{{version}}.tar.gz
    fi
    pushd gnumake
        ./configure {{gclang_config}} --prefix=$PWD/install
        make -j $(nproc)
        make install
        get-bc $PWD/install/bin/make
    popd

lua version="5.4.8":
    #!/usr/bin/env bash
    set -euxo pipefail
    if [ ! -d "lua" ]; then
        curl -fL -O https://www.lua.org/ftp/lua-{{version}}.tar.gz
        tar zxf lua-{{version}}.tar.gz
        mv lua-{{version}} lua
        rm lua-{{version}}.tar.gz
    fi
    pushd lua
        make clean
        make -j $(nproc) posix {{gclang_config}}
        make install INSTALL_TOP=$PWD/install
        get-bc $PWD/install/bin/lua
    popd

openssl version="3.5.4":
    #!/usr/bin/env bash
    set -euxo pipefail
    if [ ! -d "openssl" ]; then
        curl -fL -O https://github.com/openssl/openssl/releases/download/openssl-{{version}}/openssl-{{version}}.tar.gz
        tar zxf openssl-{{version}}.tar.gz
        mv openssl-{{version}} openssl
        rm openssl-{{version}}.tar.gz
    fi
    pushd openssl
        {{gclang_config}} ./Configure  --prefix=$PWD/install no-shared no-module enable-tls1_3 enable-rc5 enable-md2 enable-ec_nistp_64_gcc_128 enable-ssl3
        make -j $(nproc)
        make -j $(nproc) install
    popd

curl version="8.16.0":
    #!/usr/bin/env bash
    set -euxo pipefail
    if [ ! -d "curl" ]; then
        curl -fL -O https://curl.se/download/curl-{{version}}.tar.gz
        tar zxf curl-{{version}}.tar.gz
        mv curl-{{version}} curl
        rm curl-{{version}}.tar.gz
    fi
    pushd curl
        {{gclang_config}} ./configure --prefix=$PWD/install --with-openssl --with-gnutls
        make -j $(nproc)
        make -j $(nproc) install
        get-bc $PWD/install/bin/curl
    popd
